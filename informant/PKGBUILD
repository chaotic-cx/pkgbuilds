# Maintainer: Bradford Smith <aur@bradfords.me>

pkgname=informant
pkgver=0.5.1
pkgrel=1
pkgdesc="An Arch Linux News reader and pacman hook"
arch=('any')
url="https://github.com/bradford-smith94/$pkgname"
license=('MIT')
install=informant.install
makedepends=('python-setuptools')
depends=('python' 'python-docopt' 'python-dateutil' 'python-feedparser' 'python-html2text' 'python-cachecontrol' 'python-lockfile' 'python-psutil')
source=("https://github.com/bradford-smith94/$pkgname/archive/v$pkgver.tar.gz")
options=(zipman)
sha256sums=('2c88b1303fd623eef390e7bcfafeb40f45b847a7f88514726e36c66f6763c53e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  /usr/bin/python setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 0644 $pkgname.hook -T $pkgdir/usr/share/libalpm/hooks/00-$pkgname.hook
  install -Dm 0644 LICENSE -t $pkgdir/usr/share/licenses/$pkgname/
  install -Dm 0644 man/$pkgname.1 -t $pkgdir/usr/share/man/man1/
}
